// this is a local build file, used for debugging purposes only!
// YOU CANNOT CREATE DISTRIBUTIONS WITH IT
//

plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.aim42.htmlSanityCheck' version '1.1.6'
}

apply plugin: 'java'

// what language are we building
// current choices: EN, DE, IT, ES
def LANGUAGE = 'EN'

// what version of the template are we building here
// 8.1 -> add UA and CZ version
// 8.1.1 -> bugfix in EN version
// these comments should go into a decent history.adoc file...
version = "8.2-${LANGUAGE}-LOCAL-BUILD"

repositories {
    mavenCentral()
}

// not found: /Users/gernotstarke/projects/arc42/arc42-template/src/config.adoc

asciidoctor {
    baseDir file('./')
    baseDirFollowsSourceDir()

    sourceDir file("${LANGUAGE}/asciidoc")
    sources{ include 'arc42-template.adoc' }
   
    options = [
        includedir: "./${LANGUAGE}/asciidoc/",
        imagesdir: '../images'
    ]    
    
    outputDir = file( "build/${LANGUAGE}" )

    resources {
        from('./') {
            include 'images/**'
        }
    }
}

htmlSanityCheck {
    // ensure asciidoctor->html runs first
    // and images are copied to build directory
    dependsOn asciidoctor

    sourceDir = new File( "$buildDir/$LANGUAGE" )

    // files to check, specified as a file tree with filtering
    sourceDocuments = fileTree(sourceDir) {
        include "arc42-template.html"
    }

    // where to put results of sanityChecks...
    checkingResultsDir = new File( "$buildDir/reports/$LANGUAGE" )

    // fail build on errors?
    failOnErrors = true

    // http connection timeout in milliseconds
    httpConnectionTimeout = 1000

    // which statuscodes shall be interpreted as warning, error or success
    // defaults to standard
    httpWarningCodes = [401]
    // httpErrorCodes
    // httpSuccessCodes
}